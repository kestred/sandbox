/* Filename: parser.yxx */

%{
	#include "cpp.h"
	#include "lexer.h"
	#include "parser.h"

	#define yyerror cpp_yyerror
	#define yywarning cpp_yywarning

	using namespace std;

	static Module* module = NULL;
%}

/* Set Parser Options */
%glr-parser    // using glr so we can allow some conflicts in ambiguous C++ grammar
%error-verbose // has bison output verbose errors
%locations     // enable location tracking; we use this to pair comments with tokens

//%define api.namespace {treesap}
%define api.prefix {cpp_yy}
%define api.token.prefix {TOKEN_} // prefix tokens with TOKEN_ to avoid name conflicts

/* Define our YYSTYPE */
%union {
	int integer;
	bool boolean;
	char character;
	double floating;
	std::string* string;
}

/* Lexing Symbols */
%token END 0 "end of file" // error messages refer to "end of file" intead of $end

/* Literal Tokens */
%token <integer>   INTEGER
%token <floating>  FLOAT
%token <string>    STRING
%token <character> CHAR
%token <boolean>   BOOLEAN

/* Semantic Tokens */
%token <string> IDENTIFIER

/* Keyword Tokens */
%token KW_NAMESPACE "namespace"
%token KW_CONST     "const"

// Access specifiers
%token KW_PUBLIC    "public"
%token KW_PROTECTED "protected"
%token KW_PRIVATE   "private"

// Type-definitions
%token KW_ENUM     "enum"
%token KW_STRUCT   "struct"
%token KW_CLASS    "class"
%token KW_TYPEDEF  "typedef"
%token KW_TYPENAME "typename"
%token KW_TEMPLATE "template"

// Fundamental-type keywords
%token KW_INT      "int"
%token KW_CHAR     "char"
%token KW_DOUBLE   "double"
%token KW_UNSIGNED "unsigned"
%token KW_SIGNED   "signed"
%token KW_LONG     "long"
%token KW_SHORT    "short"
%token KW_BOOL     "bool"

// Compiler directives
%token KW_ATTRIBUTE "__attribute__"
%token ATTR_VISIBILITY "__visibility__"
%token ATTR_NORETURN "__noreturn__"

// Keyword-operators
%token KW_SIZEOF   "sizeof"
%token KW_DECLTYPE "decltype"

/* Operator Tokens */
%token OP_ASSIGN  "="
%token OP_EQUALS  "=="

// We're naming most ops by their symbol, because they're overload
%token OP_CONS    "::"
%token OP_ANDAND  "&&"
%token OP_AND     "&"
%token OP_BARBAR  "||"
%token OP_BAR     "|"
%token OP_STAR    "*"
%token OP_ANGLE_L "<"
%token OP_ANGLE_R ">"

/* Precedence */
%left ','
%right "throw"
%right "?:" "=" "+=" "-=" "*=" "/=" "%=" "<<=" ">>=" "&=" "^=" "|="
%left "||"
%left "&&"
%left "|"
%left "^"
%left "&"
%left "==" "!="
%left "<" "<=" ">" ">="
%left "<<" ">>"
%right "+" "-"
%right "*" "/" "%"
%right "!" "~"  '(' ')' "new" "new[]" "delete" "delete[]" "sizeof"
%left "()" '[' ']' '.' "->"
%left "::"

%%

cxx : empty
	| declarations
	| error ';'
	;

declarations
	: declaration
	| declarations declaration
	;

declaration
	: namespace_decl
	| method_decl ';'
	| class_decl ';'
	| struct_decl ';'
	| enum_decl ';'
	| typedef_decl ';'
	| variable_decl ';'
	;

statements
	: statement
	| statements statement
	;

statement
	: value_statement ';'
	;

value_statement
	: qualified_name
	| raw_value
	| boolean_value
	| cast_value
	| type_initializer
	| '(' value_statement ')'
	;

boolean_value
	: value_statement "==" value_statement
	| value_statement "!=" value_statement
	| value_statement "<=" value_statement
	| value_statement ">=" value_statement
	| value_statement "<" value_statement
	| value_statement ">" value_statement
	| value_statement "||" value_statement
	| value_statement "&&" value_statement
	| "!" value_statement
	;

type_initializer
	: type_name '(' argument_list ')'
	| "new" type_name '(' argument_list ')'
	;

cast_value
	: '(' type_name variable_prefix ')' value_statement
	;

argument_list
	: value_statement
	| argument_list ',' value_statement
	;

class_body
	: class_part
	| class_body class_part
	;

class_part
	: declaration
	| access_specifier
	;

access_specifier
	: "public" ':'
	| "protected" ':'
	| "private" ':'
	;

enumeration
	: enum_element
	| enumeration ',' enum_element
	;

enum_element
	: IDENTIFIER
	| IDENTIFIER "=" value_statement
	;

variable_decl
	: type_name variable_init
	| variable_decl ',' variable_init
	;

variable_init
	: variable_spec
	| variable_spec '=' statement
	;

enum_decl
	: "enum" IDENTIFIER '{' '}'
	| "enum" IDENTIFIER '{' enumeration '}'
	| "enum" '{' enumeration '}'
	;

template_decl
	: "template" "<" template_type_list ">" 
	;

template_type_list
	: template_type
	| template_type_list ',' template_type
	;

template_type
	: template_type_identifier
	| template_type_identifier "=" template_instance_type
	;

template_type_identifier
	: "class"
	| "typename"
	| "class" IDENTIFIER
	| "typename" IDENTIFIER
	;

template_instance_type
	: type_qualified_name "<" type_list ">"
	| type_qualified_name "<" raw_value_list ">"
	;

template_spec
	: "template" "<" ">"
	;

template_spec_with_instance_type
	: "template" "<" type_list ">"
	;

class_decl
	: class_identifier         // declaration
	| class_identifier '{' '}' // definition
	| class_identifier '{' class_body '}'
	;

class_identifier
	: "class" IDENTIFIER
	| template_decl "class" IDENTIFIER
	| template_spec "class" template_instance_type
	| template_spec_with_instance_type "class" IDENTIFIER
	;

struct_decl
	: struct_identifier         // declaration
	| struct_identifier '{' '}' // definition
	| struct_identifier '{' class_body '}'
	;

struct_identifier
	: "struct" IDENTIFIER
	| template_decl "struct" IDENTIFIER
	| template_spec "struct" template_instance_type
	| template_spec_with_instance_type "struct" IDENTIFIER
	;

typedef_decl
	: "typedef" type_name variable_spec
	;

namespace_decl
	: "namespace" attribute_directive '{' '}'
	| "namespace" attribute_directive '{' declarations '}'
	| "namespace" IDENTIFIER attribute_directive '{' '}'
	| "namespace" IDENTIFIER attribute_directive '{' declarations '}'
	;

method_decl
	: type_name variable_spec '(' ')' method_body attribute_directive
	| type_name variable_spec '(' method_params ')' method_body attribute_directive
	;

method_params
	: method_param
	| method_params ',' method_param
	;

method_param
	: type_name variable_with_optional_name
	;

variable_with_optional_name
	: variable_prefix
	| variable_spec
	;

attribute_directive
	: empty
	| "__attribute__" '(' '(' attribute_list ')' ')'
	;

attribute_list
	: compiler_attribute 
	| attribute_list ',' compiler_attribute
	;

compiler_attribute
	: "__visibility__" '(' STRING ')'
	| "__noreturn__"
	;

method_body
	: empty
	| '{' statements '}'
	;

variable_spec
	: variable_prefix IDENTIFIER
	;

variable_prefix
	: empty
	| "&"
	| "*" variable_prefix
	;

type_name
	: integer_type
	| "bool"
	| qualified_name
	| type_qualified_name
	| template_instance_type
	| "decltype" '(' IDENTIFIER ')'
	| type_const_name
	;

type_qualified_name
	: qualified_name
	| "typename" qualified_name
	;

type_const_name
	: "const" integer_type
	| "const" type_qualified_name
	| "const" template_instance_type
	;

type_list
	: type_name
	| type_list ',' type_name
	;

raw_value_list
	: raw_value
	| raw_value ',' raw_value_list
	;

raw_value
	: BOOLEAN
	| INTEGER
	| FLOAT
	| CHAR
	| STRING { delete $1; }
	;

// For this rule, we'll allow almost everything in the grammar, but manually
//     check to make sure the user isn't insane.
integer_type
	: "short"
	| "char"
	| "long"
	| "int"
	| "unsigned"
	| "signed"
	| integer_type "char"
	| integer_type "short"
	| integer_type "long"
	| integer_type "int"
	| integer_type "unsigned"
	| integer_type "signed"
	;

qualified_name
	: IDENTIFIER
	| qualified_name "::" IDENTIFIER
	;

/*
string_literal
	: STRING
	| string_literal STRING
	;
*/

empty
	:
	;

%%

/* ---- User Subroutines ---- */
Module* run_parser(const std::string & filename) {
	module = init_lexer(filename);
	if(!module) { return NULL; }
	if(yyparse() != 0 || cpp_yyerrors() > 0) { return NULL; }
	return module;
}
int parser_errors() { return cpp_yyerrors(); }
int parser_warnings() { return cpp_yywarnings(); }
